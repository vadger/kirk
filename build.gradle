buildscript {
    ext.kotlinVersion = '1.1.4-2'

    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
    }
}


subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'
    apply plugin: 'maven'
    apply plugin: 'com.bmuschko.nexus'
    apply plugin: 'signing'

    repositories {
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    test {
        useTestNG()
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }

    jacoco {
        toolVersion = "0.7.2.201409121644"
    }

    jacocoTestReport {
        executionData = files("${project.buildDir}/jacoco/test.exec")
        reports {
            xml.destination "${buildDir}/reports/jacoco/report.xml"
            xml.enabled true
            html.enabled true
            html.destination "${buildDir}/jacocoHtml"
        }
    }

    modifyPom {
        project {
            name 'com.automation.remarks.kirk'
            packaging 'jar'
            description 'Kirk - pragmatic UI test automation'
            url 'https://github.com/SergeyPirogov/kirk'

            scm {
                url 'scm:git@github.com:SergeyPirogov/kirk.git'
                connection 'scm:git@github.com:SergeyPirogov/kirk.git'
                developerConnection 'scm:git@github.com:SergeyPirogov/kirk.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'SerhiiP'
                    name 'Sergey Pirogov'
                }
            }
        }
    }

    extraArchive {
        sources = true
        tests = false
        javadoc = true
    }

    nexus {
        sign = true
    }

    task listAllDependencies(type: DependencyReportTask) {}
}

apply plugin: 'org.asciidoctor.convert'

archivesBaseName = baseArchiveName

repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

asciidoctor {
    //backends 'pdf'
    sourceDir = file('docs/source')
    outputDir = file('build/docs')
    attributes   \
             'VERSION': "$version",
            'source-highlighter': 'coderay',
            'imagesdir': './images',
            'toc': 'left',
            'toclevels': '4',
            'icons': 'font',
            'setanchors': '',
            'idprefix': '',
            'sectnums': '',
            'idseparator': '-',
            'docinfo1': ''
}

asciidoctor.doLast {
    copy {
        from 'build/docs/html5'
        into "$projectDir/docs"
        include '*.html'
    }
}
